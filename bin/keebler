#!/usr/bin/env -S nu --stdin

source ../data/keymap.nu

# Process all layers and keys into QMK keycodes.
def "build codes" [ctx: record] {
	let layers = $ctx
	| get layers
	| each {|layer|
		let codes = get keys
		| compact
		| each {|key|
			into string
			| main map
		}

		$layer | upsert codes $codes
	}
	$ctx | upsert layers $layers
}

# Generate QMK code for each layer.
def "build keymaps" [ctx: record] {
	let layers = $ctx
	| get layers
	| each {|layer|
		let keymap = $layer.codes
		| str join ", "
		| $"LAYOUT\(($in))"

		$layer
		| upsert id $"_($layer.name | str upcase)"
		| upsert keymap $keymap
	}
	$ctx | upsert layers $layers
}

# Generate QMK file.
def "build qmk" [ctx: record] {
	$ctx
	| to json
	| tera --stdin --template ./templates/keymap.c
	| save --force ./keymap.c
}

def main [] {
	open data/keebler.nuon
	| build codes $in
	| build keymaps $in
	| to json
}

# Map a key string into a QMK keycode.
def "main map" [
	# in: string # key
	--fn (-f) # whether key is a function
] {
	let key = $in | str trim
	let parts = $key | parse "{fn}[{args}]" | get -i 0

	if $fn and $"($key)_t" in $keymap {
		$keymap | get $"($key)_t"
	} else if $key in $keymap {
		$keymap | get $key
	} else if ($parts | is-not-empty) {
		let fn = $parts.fn | main map --fn
		let args = $parts.args
		| split column ","
		| str trim
		| get 0
		| values
		| each {
			main map
		}
		| str join ", "
    $"($fn)\(($args))"
	} else if ($fn or ($key | str starts-with '_')) {
		$key | str upcase
	} else {
		"KC_" ++ $key | str upcase
	}
}

# Generate QMK file.
def "main build" [] {
	open data/keebler.nuon
	| build codes $in
	| build keymaps $in
	| build qmk $in
}

# Generate keymap.c.
def "main compile" [] {
	main build
	qmk compile -j 32 -kb sofle_pico -km ermak
}

# Flash QMK firmware.
def "main flash" [] {
	mount /dev/disk/by-label/RPI-RP2 /mnt/usb
	cp ~/qmk_firmware/sofle_pico_ermak.uf2 /mnt/usb
}

# Generate QMK file.
def "main watch compile" [] {
	watch . {|op, path|
		if $path in (glob "{bin,data,templates}/*") {
			echo "Compiling board..."
			main compile
		}
	}
}

# Generate QMK file.
def "main watch flash" [] {
	watch /dev/disk/by-label {|op, path|
		if ($op == "Create" and ($path | str ends-with "/RPI-RP2")) {
			echo "Flashing board..."
			main flash
		}
	}
}
